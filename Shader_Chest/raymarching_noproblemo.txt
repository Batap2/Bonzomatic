#version 410 core

uniform float t; // in seconds
uniform vec2 v2Resolution; // viewport resolution (in pixels)
uniform float fFrameTime; // duration of the last frame, in seconds

uniform sampler1D texFFT; // towards 0.0 is bass / lower freq, towards 1.0 is higher / treble freq
uniform sampler1D texFFTSmoothed; // this one has longer falloff and less harsh transients
uniform sampler1D texFFTIntegrated; // this is continually increasing
uniform sampler2D texPreviousFrame; // screenshot of the previous frame
uniform sampler2D texChecker;
uniform sampler2D texNoise;
uniform sampler2D texTex1;
uniform sampler2D texTex2;
uniform sampler2D texTex3;
uniform sampler2D texTex4;

in vec2 out_texcoord;
vec2 uv;
layout(location = 0) out vec4 out_color; // out_color must be written in order to see anything

const int MAX_MARCHING_STEPS = 100;
const float MIN_DIST = 0.0f;
const float MAX_DIST = 1000;
const float EPSILON = 0.0001;
const float PI = 3.141592;

float rand(vec2 n) { 
	return fract(sin(dot(n, vec2(12.9898, 4.1414))) * 43758.5453);
}

float noise(vec2 p){
	vec2 ip = floor(p);
	vec2 u = fract(p);
	u = u*u*(3.0-2.0*u);
	
	float res = mix(
		mix(rand(ip),rand(ip+vec2(1.0,0.0)),u.x),
		mix(rand(ip+vec2(0.0,1.0)),rand(ip+vec2(1.0,1.0)),u.x),u.y);
	return res*res;
}


vec2 pMod2(inout vec2 p, float size){
	float halfsize = size*0.5;
	vec2 c = floor((p+halfsize)/size);
	p = mod(p+halfsize,size)-halfsize;
	return c;
}

float sdSphere( vec3 p, float s )
{
  return length(p)-s;
}

float map(vec3 p){
    vec2 index = pMod2(p.xz, 5.0);
    float valNoise = noise(index);
    p.y -= valNoise * 14.0;
    float pulse = (sin(t * length(index)) + 1.0) / 4.0;
	return sdSphere(p - vec3(0,10,0), valNoise + 0.2 + pulse);
}

float raymarch(vec3 ro, vec3 rd){
	float t = 0.0;
    for(int i = 0; i < MAX_MARCHING_STEPS; ++i){
        vec3 p = ro + rd * t; 
    	float d = map(p);
        
        if (d < EPSILON){
        	break;
        }
        
        t += min(d, 2.5);
    }
    return t;
}

mat3 setCamera( in vec3 ro, in vec3 ta, in float cr )
{
	vec3 cw = normalize(ta-ro);
	vec3 cp = vec3(sin(cr), cos(cr),0.0);
	vec3 cu = normalize( cross(cw,cp) );
	vec3 cv = normalize( cross(cu,cw) );
    return mat3( cu, cv, cw );
}

void main(void)
{
    vec2 uv = gl_FragCoord.xy/v2Resolution.xy;
    uv = uv * 2.0 - 1.0;
    uv.x *= v2Resolution.x / v2Resolution.y;
    
    vec3 ro = vec3(12.0*cos(t / 3.0), 3.0,12.0*sin(t / 3.0) );
	vec3 ta = vec3(0.0,4.0, 0.0);

    mat3 cam = setCamera(ro, ta, 0.0);
    vec3 rd = cam * normalize(vec3(uv,2.0));
    
    float dist = raymarch(ro, rd);
    
    float fog = 1.0 / (1.0 + dist * dist * 0.001);
    
    out_color = vec4(fog * vec3(1.0, 0.75, 0.0),1.0);
}